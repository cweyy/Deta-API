plugins {
    id 'java'
    id "maven-publish"
    id 'signing'
}

group 'io.github.cweyy'
version '1.0'

repositories {
    mavenCentral()
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.1'
    implementation group: 'com.google.code.gson', name: 'gson', version: '2.7'
    implementation group: 'org.apache.httpcomponents', name: 'httpclient', version: '4.5.14'
}

test {
    useJUnitPlatform()
}

java {
    withSourcesJar()
    withJavadocJar()
}


publishing {
    repositories {
        maven {
            name = "OSSRH"
            url = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            credentials {
                username = System.getenv("OSSRH_USERNAME")
                password = System.getenv("OSSRH_PASSWORD")
            }
        }
    }

    publications {
        mavenJava(MavenPublication) {
            artifactId 'deta-api'
            from(components.java)

            pom {
                name = 'Deta-API'
                description = 'unofficial deta.sh/deta.space api for java'
                url = 'https://github.com/cweyy/Deta-API'

                scm {
                    connection = 'scm:git:git:github.com/cweyy/Deta-API.git'
                    developerConnection = 'scm:git:ssh://github.com:cweyy/Deta-API'
                    url = 'https://github.com/cweyy/Deta-API'
                }

                developers {
                    developer {
                        id = 'cweyy'
                        name = 'Johann Judek'
                        email = 'johannjudek@icloud.com'
                    }
                }

                licenses {
                    license {
                        name = 'MIT License'
                        url = 'http://www.opensource.org/licenses/mit-license.php'
                    }
                }
            }


        }
    }
}

signing {
    sign publishing.publications.mavenJava


}